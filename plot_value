#!/usr/bin/gnuplot -c
#
# This plots all the instantanous value metrics on the one plot.
# Scaling has been applied to make all the values comparable and
# visible without zooming.

# Originally used this shortcut
#gnuplot-time \
#    'f:$3/1000000' 'f:$4/1000000' \
#    'f:$5/1000000' 'f:$6/1000000' \
#    'f:$7/300' 'f:$8/100' \
#    9 10 11 12 13 14 15 16 \
#    ARG1

set timestamp 'generated at %F %T'
set title 'All error seconds values'
set xlabel 'Time (GMT)'
set ylabel 'Value'

#set xtics out rotate by -15 86400
set xtics out rotate by -15
#set mouse clipboardformat 5
set grid ytics mytics lw 2, lw 1
set grid xtics
set autoscale fix

set key left top Left title 'Legend' box 3

set timefmt '%s'
set xdata time
set format x '%Y-%m-%d %H:%M'
set mouse mouseformat 5
set style data lines

plot \
    ARG1 using 1:($3/1000000)  title '(data_rate_down/1000000)',\
    ARG1 using 1:($4/1000000)  title '(data_rate_up/1000000)',\
    ARG1 using 1:($5/1000000)  title '(max_data_rate_down/1000000)',\
    ARG1 using 1:($6/1000000)  title '(max_data_rate_up/1000000)',\
    ARG1 using 1:($7/300)  title '(latency_down/300)',\
    ARG1 using 1:($8/100)  title '(latency_up/100)',\
    ARG1 using 1:9  title 'line_attenuation_down',\
    ARG1 using 1:10  title 'line_attenuation_up',\
    ARG1 using 1:11  title 'signal_attenuation_down',\
    ARG1 using 1:12  title 'signal_attenuation_up',\
    ARG1 using 1:13  title 'noise_margin_down',\
    ARG1 using 1:14  title 'noise_margin_up',\
    ARG1 using 1:15  title 'actatp_down',\
    ARG1 using 1:16  title 'actatp_up'
pause mouse close

